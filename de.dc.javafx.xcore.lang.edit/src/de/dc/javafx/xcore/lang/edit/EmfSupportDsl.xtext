grammar de.dc.javafx.xcore.lang.edit.EmfSupportDsl with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate emfSupportDsl "http://www.dc.de/javafx/xcore/lang/edit/EmfSupportDsl"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase

Model:
	ecore+=Ecore*;

Ecore:
'EMFSupport' 'for' name=EString
'{'
	'packagePath:' packagePath=EString ','?
	'modelItemProviderAdapterFactory:' modelItemProviderAdapterFactory=JvmTypeReference ','?
	'modelPackage:' modelPackage=JvmTypeReference ','?
	'modelFactory:' modelFactory=JvmTypeReference ','?
	'rootType:' rootType=JvmTypeReference ','?
	'useUndoRedo:' useUndoRedo=EBoolean ','?
	'showCommandStack:' showCommandStack=EBoolean ','?
	'showPropertyView:' showPropertyView=EBoolean ','?
	'generateDemo:' generateDemo=EBoolean ','?
	
	('editables' '+=' editables+=EditableEAttributes ','?)*
	('contextMenus' '+=' contextMenus+=ContextMenu ','?)*
	('controls' '+=' supportedControls+=SupportedControl ','?)*
'}'	
;

EditableEAttributes:
	'EAttribute' '(' name=EString ')'
;

ContextMenu:
	AddContextMenu | CopyContextMenu
;

CopyContextMenu:
	'copyContextMenu' '('
		'id:' id=EString ','?
	')'
;

AddContextMenu:
	'addContextMenu' '('
		'id:' id=EString ','?
		'parentType:' parentType=JvmTypeReference ','?
		'createType:' createType=JvmTypeReference ','?
	')'
;

SupportedControl:
	TableViewFX | TreeViewFX | ListViewFX
;

TreeViewFX:
	'TreeView' '(' 'name:' name=EString  ','? 'model:' model=JvmTypeReference ')'
;

ListViewFX:
	'ListView' '(' 'name:' name=EString  ','? 'model:' model=JvmTypeReference ')'
;

TableViewFX:
	'TableView' '(' 'name:' name=EString ','? 'model:' model=JvmTypeReference ')'
;

TreeTable:
	'TreeTableView' '(' 'name:' name=EString ')'
;
	
EBoolean returns ecore::EBoolean:
	'true' | 'false';
	
EInt returns ecore::EInt:
	'-'? INT;

EString returns ecore::EString:
	STRING | ID;