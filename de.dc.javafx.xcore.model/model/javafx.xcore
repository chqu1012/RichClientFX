@Ecore(nsURI="http://www.frateranatis.org/javafx")
@GenModel(multipleEditorPages="false", creationIcons="false", 
	editDirectory="/de.dc.javafx.xcore.model.edit/src-gen",
	editorDirectory="/de.dc.javafx.xcore.model.editor/src-gen", 
	richClientPlatform="true", 
	codeFormatting="true",
	importerID="org.eclipse.emf.importer.ecore", 
	runtimePlatform="RCP")
package de.dc.emf.javafx.model.javafx

import org.eclipse.xtext.common.types.JvmTypeReference

class ProjectFX extends NamedElement {
	contains ControlFX[] controls
	String packagePath
	contains ModelFX[] models
	contains Binding[] bindings
	contains ChartFX[] charts
}

abstract class ControlFX extends NamedElement {
	boolean generateDemo = 'false'
}

class TableColumnFX extends NamedElement {
	int width = "200"
	refers AttributeFX usedAttribute
	contains JvmTypeReference[0..1] cellValueFactory
	boolean useFilter
}

abstract class ModelFX extends NamedElement {
	contains AttributeFX[] attributes
}

class AttributeFX extends NamedElement {
	String ^type
}

class Bean extends ModelFX {
}

class DerivedBean extends ModelFX {
	String instanceType
}

class Binding {
	String name
	contains BindingProperty[] property
}

class BindingProperty {
	String name
	BindingType ^type
}

class TableViewFX extends ControlFX {
	boolean showPropertyView = 'true'
	boolean showToolbar = 'true'
	contains TableColumnFX[] columns
	contains JvmTypeReference[0..1] usedModel
}

class TreeViewFX extends ControlFX {
	boolean showPropertyView = 'true'
	contains JvmTypeReference[0..1] usedModel
}

enum BindingType {
	StringProperty
	IntegerProperty = 1
	DoubleProperty = 2
	FloatProperty = 3
	BooleanProperty = 4
	ObjectProperty = 5
	ObservableList = 6
}

class NamedElement {
	String name
}

class FilteredElement {
	boolean useFilter
}

abstract class ChartFX extends NamedElement {
	String title = " "
	Orientation titleSide = "Top"
	boolean showLegend = "true"
	Orientation legendSide = "Bottom"
	contains ChartSeries[] series
}

abstract class XYChartFX extends ChartFX{
	AxisType xAxisType
	AxisType yAxisType
	String xAxisLabel
	String yAxisLabel
}

class ChartSeries extends NamedElement {
	contains ChartFXData[] dataList
}

class ChartFXData {
	String xValue
	String yValue
}

class LineChartFX extends XYChartFX {
}

class PieChartFX extends ChartFX {
}

class AreaChartFX extends XYChartFX {
}

class BubbleChartFX extends XYChartFX {
}

class ScatterChartFX extends XYChartFX {
}

class BarChartFX extends ChartFX {
}

enum Orientation {
	Left
	Right = 1
	Top = 2
	Bottom = 3
}

enum AxisType {
	Number
	Category = 1
}